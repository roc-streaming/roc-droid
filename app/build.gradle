def getVersionName = { ->
    def manifest = new XmlSlurper().parse(file('src/main/AndroidManifest.xml'))
    def manifestName = manifest.@'android:versionName'.text()

    def gitStdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--abbrev=0'
        standardOutput = gitStdout
    }
    def gitTag = gitStdout.toString().trim().replaceAll('v', '')

    if (manifestName != gitTag) {
        throw new GradleException(
            "Mismatched android:versionName in AndroidManifest.xml and git tag: "+
                "versionName = $manifestName, "+
                "gitTag = $gitTag")
    }

    return manifestName
}

def getVersionCode = { ->
    def manifest = new XmlSlurper().parse(file('src/main/AndroidManifest.xml'))
    def manifestName = manifest.@'android:versionName'.text()
    def manifestCode = manifest.@'android:versionCode'.text()

    def expectedCode = 0
    def mult = 1
    manifestName.tokenize('.').reverse().each {
        expectedCode += it.toInteger() * mult
        mult *= 1000
    }

    if (manifestCode != expectedCode.toString()) {
        throw new GradleException(
            "Mismatched android:versionName and android:versionCode in AndroidManifest.xml: "+
                "versionName = $manifestName, "+
                "versionCode = $manifestCode, "+
                "expectedCode = $expectedCode")
    }

    return manifestCode.toInteger()
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion project.compileSdkVersion.toInteger()
    buildToolsVersion project.buildToolsVersion
    ndkVersion project.ndkVersion

    defaultConfig {
        applicationId 'org.rocstreaming.rocdroid'
        minSdkVersion project.minSdkVersion.toInteger()
        targetSdkVersion project.targetSdkVersion.toInteger()
        versionName getVersionName()
        versionCode getVersionCode()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
               'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'org.roc-streaming.roctoolkit:roc-android:0.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.2.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
